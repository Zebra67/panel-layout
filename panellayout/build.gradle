apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.gradle.maven-publish'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig = false
        resValues = false
    }
}

androidExtensions {
    features = ['parcelize']
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "androidx.transition:transition:1.3.1"
}

publishing {
    publications.create("default", MavenPublication) {
        afterEvaluate {
            from(components.findByName("release"))
        }

        def sourcesJarTask = tasks.create("sourcesJar", Jar) {
            archiveClassifier.set("sources")
            from(android.sourceSets["main"].java.srcDirs)
        }
        artifact(sourcesJarTask)

        groupId = findProperty("GROUP")
        version = findProperty("VERSION_NAME")
        artifactId = findProperty("POM_ARTIFACT_ID")

        pom {
            name = findProperty("POM_NAME")
            description = findProperty("POM_DESCRIPTION")
            packaging = findProperty("POM_PACKAGING")
            url = findProperty("POM_URL")

            scm {
                url = findProperty("POM_SCM_URL")
                connection = findProperty("POM_SCM_CONNECTION")
            }

            licenses {
                license {
                    name = findProperty("POM_LICENCE_NAME")
                    url = findProperty("POM_LICENCE_URL")
                }
            }
        }
    }

    repositories {
        maven {
            name = "bintray"
            url = uri("https://api.bintray.com/maven/wayfair/panellayout/panellayout/;publish=1;override=1")
            credentials {
                username = System.getenv("BINTRAY_USER")
                password = System.getenv("BINTRAY_API_KEY")
            }
        }
    }
}
